# Exercise 2.28

Write a procedure `fringe` that takes as argument a tree (represented as a list)
and returns a list whose elements are all the leaves of the tree arranged in
left-to-right order. For example,

```scheme
(define x
  (list (list 1 2) (list 3 4)))

(fringe x)
(1 2 3 4)

(fringe (list x x))
(1 2 3 4 1 2 3 4)
```

## Note

Once completed this, I really can't see how could I need so much time for this
exercise (3h). It's so simple. I guess the habit of handling array/list from
first to last has been ingrained deeply into my brain, after too many Leetcode
problems. This would be a good interview question. And, unsurprisingly, there's
a LeetCode problem for this:

- https://leetcode.com/problems/flatten-nested-list-iterator

## Answer

This question basically asks to implement the [`flatten`][flatten] function.

This is lengthy. Even though it looks simple, I got some headache thinking about
it and many mistakes. So, I want to use writing to clear my thought. Hope this
will be helpful to other learners.

For the impatient: [`./2.28.fringe.rkt`](./2.28.fringe.rkt)

### Naive

Walk through the tree, for each node

- If it's `null` (node is `(cons a (cons b (cons...)))` in Lisp), return `null`.
- If it's not a `cons`, we have a value. Just return a `list` of single element,
  so that the outer `append` can work with it.
- Otherwise, `append` the result of `fringe` the head of the node (`car tree`)
  and the rest of its (`cdr tree`).

```scheme
(define (fringe tree)
  (cond
    [(null? tree) null]
    [(not (cons? tree)) (list tree)]
    [else
     (append (fringe (car tree))
             (fringe (cdr tree)))]))
```

This solution works, but terribly inefficient. For each single non-`cons`
element:

- `fringe` is called $O(height(tree))$ times. We can't simply say $O(log_k(n))$,
  because there's no limit $k$ for number of children each node can have.

- `append` is also called $O(height(tree))$ times with tons of intermediate
  smaller lists. Think about all the garbage collector cost.

We need to design a better solution.

### `cons`-up

It would be must simpler if we could maintain a _pointer_ to the tail of the
list (i.e. `null` cell), point it to the next list, so that we can "append"
without walking through both 2 sides, thus, eliminate the need for intermediate
values.

We could do that with [`set!`][set] (yet to be introduced in the book, I'm also
not sure if `set!` is available in Scheme). However, that goes against the idea
of pure function (only computation, no side effect), which is the main theme til
this point.

I believe this exercise have a better solution that doesn't require mutable
list, as it is placed before any discussion on mutable data structure.

Let's think. We can't reduce amount of recursion steps, as we need to walk deep
into the tree. But, we don't have to call `fringe` on each step, so, we are not
limited to the data structured returned by `fringe`.

We don't want to `append`, we want to build our result using `cons` directly.
Thus, we need to be able to write

```scheme
(define (fringe tree)
    ; ...
    (cons v result)
    ; ...
  )
```

What is `result`: the final list. Where is `v` come from. It's `(car tree)`.
But, `v` could be either `null`, primitive or `list`. We don't want to `cons`
non-primitive with the result. Thus, we need some check.

```scheme
(cond
  [(null? v) result]                ; end of whatever list we are processing.
  [(not (pair? v)) (cons v result)] ; found a primitive, `cons` it up.
  [else ... ])                      ; what to do here?
```

In the `else` case, we need to continue recursively go deeper and build up the
`result` from there, then use the returned value to continue in `else` branch.

Let's call our inner function `walk`.

```scheme
(define (walk node result) ...)
```

The name is changed from `v` to `node`, because we now know better what is `v`.

In `else` case:

- Go deeper one level: `(walk (car node) result)`
- Use the returned value as our new `result`: `(walk (cdr node) <returned>)`

Putting them together:

```scheme
(define (fringe tree)
    (define (walk node result)
      (cond ...
        [else (walk
                (cdr node)
                (walk (car node) result)))])
   ; using `walk`
  )
```

Note that while walking the tree, we see our value in their order, and we're
building the final list by putting each value at the beginning of the current
`result`. Hence, `result` is in reversed order. We need to `reverse` it again.

The better solution

```scheme
(define (fringe tree)
  (define (walk node result)
    (cond
      [(null? node) result]
      [(not (pair? node)) (cons node result)]
      [else (walk
              (cdr node)
              (walk (car node) result))]))
  (reverse (walk tree null)))
```

We need `reverse` because of how we `cons` those value: first to last. What if
we could `cons` it in the reversed order, from last to first?

We want change the below part to use `node` in reversed order.

```scheme
(walk
  (cdr node)
  (walk (car node) result))
```

Let's use `head` and `tail` and pseudocode to clarify what happen in the `else`
branch.

```scheme
else:
  head = (car node)
  tail = (cdr node)
  new_result = (walk head result)
  return (walk tail new_result)
```

Obviously, we want to process `tail` before `head`. So, let's change to:

```scheme
else:
  head = (car node)
  tail = (cdr node)
  new_result = (walk tail result)
  return (walk head new_result)
```

The best solution

```scheme
(define (fringe tree)
  (define (walk node result)
    (cond
      [(null? node) result]
      [(not (pair? node)) (cons node result)]
      [else (walk
              (car node)                   ; process head
              (walk (cdr node) result))])) ; process tail
  (walk tree null))
```

[flatten]:
  https://docs.racket-lang.org/reference/pairs.html#%28def._%28%28lib._racket%2Flist..rkt%29._flatten%29%29
[set]:
  https://docs.racket-lang.org/reference/set_.html#%28form._%28%28quote._~23~25kernel%29._set%21%29%29
