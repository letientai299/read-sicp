# Exercise 2.56

Show how to extend the basic differentiator to handle more kinds of expressions.
For instance, implement the differentiation rule

$$
\frac{d(u^n)}{dx} = nu^{n-1} \frac{du}{dx}
$$

by adding a new clause to the `deriv` program and defining appropriate
procedures `exponentiation?`, `base`, `exponent`, and `make-exponentiation`.
(You may use the symbol `**` to denote exponentiation.) Build in the rules that
anything raised to the power 0 is 1 and anything raised to the power 1 is the
thing itself.

## Answer

Let's do the harder variation of this exercise. We will try to support
$\frac{d u^v}{dx}$ using these knowledge:

$$
\begin{split}
\frac{d}{dx} ln(u) = \frac{u'}{u} \\
\frac{d}{dx} e^u  = u' e^u
\end{split}
$$

Here, $ln$ is the natural logarithm function, the single quote notation denotes
$u' = \frac{du}{dx}$. Combine above 2 rules, we have

$$
\begin{split}
\frac{d}{dx} u^v &= \frac{d}{dx} e^{ln(u)v} \\
                 &= e^{ln(u) v} \frac{d}{dx} ln(u) v \\
                 &= u^v (v'ln(u) + v\frac{u'}{u})
\end{split}
$$

### Design

To support this general exponentiation form, we will need these new symbolic
expression primitives:

- `(exponentiation? exp)`: is `exp` an exponentiation, i.e. $u^v$
- `(base exp)`: returns $u$ in the exponentiation $u^v$
- `(power exp)`: returns $v$ in the exponentiation $u^v$
- `(ln? exp)`: is `exp` a natural logarithm application, i.e. $ln(u)$
- `(arg exp)`: returns the argument of $ln$.
- `(make-division u v)`: expressing $\frac{u}{v}$
- `(division? exp)`: is `exp` a division
- `(dividend exp)`: return $u$ in $\frac{u}{v}$
- `(divisor exp)`: return $v$ in $\frac{u}{v}$

Note that we _intentionally_ use generic name like `base` and `arg`, because we
envision that they will be extended to support more math definition in later
exercises.

- `base` could be used to extract $b$ from $log_b{u}$, where $log$ is the
  general logarithm function.
- `arg` could be used support any unary math function, e.g. $sin$, $cos$, ...

Since we need `division`, we might as well make `deriv` support it via:

$$
\frac{d}{dx} \frac{u}{v} = \frac{v u' - v'u}{v^2}
$$

### Code change

I'm using `^` for exponent instead of `**`. Below diff is a part of
[`bf50414`][gh-url].

[gh-url]: https://github.com/letientai299/read-sicp/commit/bf50414

```diff
+(define (make-exponentiation base power)
+  (cond
+    [(=number? base 0) (if (= power 0) 1 0)]
+    [(=number? base 1) 1]
+    [(=number? power 0) 1]
+    [(=number? power 1) base]
+    [else (list '^ base power)]))
+
+(define (exponentiation? e)
+  (and (pair? e) (eq? '^ (car e))))
+
+(define (base e)
+  (cadr e))
+
+(define (power e)
+  (caddr e))
+
+(define (make-ln exp)
+  (cond
+    [(symbol? exp)
+     ; if `exp` is a symbol, then return 1 if it's the constant `e`,
+     ; otherwise, form the natural logarithm expression.
+     (if (eq? exp 'e) 1 (list 'ln exp))]
+
+    ; if `exp` is not a symbol, we check if it's a number and handle some
+    ; special case.
+    [(number? exp)
+     (cond
+       [(= 1) 0]
+       [(> 0) (list 'ln exp)]
+       [else
+        (error "ln() is not defined for non positive number"
+               exp)])]
+
+    [else (error "unknown expression type: ln()" exp)]))
+
+(define (ln? e)
+  (and (pair? e) (eq? 'ln (car e))))
+
+(define (arg e)
+  (cadr e))
+
+(define (division? e)
+  (and (pair? e) (eq? '/ (car e))))
+(define (dividend e)
+  (cadr e))
+(define (divisor e)
+  (caddr e))
+
+(define (make-division dividend divisor)
+  (cond
+    [(=number? divisor 0) (error "can't divide for zero")]
+    [(=number? dividend 0) 0]
+    [else (list '/ dividend divisor)]))
+
 ;-----------------------------------------------------------
 ; Compute re derivative of the given function
 ;-----------------------------------------------------------
@@ -68,6 +124,21 @@
        (make-sum (make-product a (d b))
                  (make-product b (d a))))]

+    [(division? exp)
+     (let ([u (dividend exp)] [v (divisor exp)])
+       (make-division
+        (make-sum (make-product v (d u))
+                  (make-product -1 (make-product u (d v))))
+        (make-exponentiation v 2)))]
+
+    [(ln? exp)
+     (let ([u (arg exp)]) (make-division (d u) u))]
+
+    [(exponentiation? exp)
+     (let ([u (base exp)] ;
+           [v (power exp)])
+       (make-product exp (d (make-product v (make-ln u)))))]
+
     [else (error "unknown expression type: DERIV" exp)]))
```

### Test

```scheme
(deriv '(ln (+ 2 x)) 'x)
> '(/ 1 (+ 2 x))
```

$$
\frac{d}{dx} ln(x + 2) = \frac{1}{x+2}
$$

```scheme
(deriv '(^ x 2) 'x)
> '(* (^ x 2) (* 2 (/ 1 x)))
```

$$
\frac{d}{dx} x^2 = x^2 \frac{2}{x}
$$

```scheme
(deriv '(/ (^ x 2) (+ x 1)) 'x)
> (/
     (+
        (*
           (+ x 1)
           (*
              (^ x 2)
              (* 2 (/ 1 x))))
        (* -1 (^ x 2)))
     (^ (+ x 1) 2))
```

$$
\frac{d}{dx} \frac{x^2}{x+1} = \frac{
(x + 1)(x^2 \frac{2}{x})
+
(-1 \cdot x^2)
}{(x+1)^2}
$$

It looks really complicated, but after some manual simplification, we get the
expected result

$$
\frac{x(x+2)}{(x+1)^2}
$$

We will try to simplify the result in [Exercise 2.57](./2.57.md), which asks for
arbitrary numbers of terms for `sum` and `product`. That will reduce amount of
parentheses in the result above.
