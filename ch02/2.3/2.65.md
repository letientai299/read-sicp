# Exercise 2.65

Use the results of [Exercise 2.63](./2.63.md) and [Exercise 2.64](./2.64.md) to
give $\Theta(n)$ implementations of `union-set` and `intersection-set` for sets
implemented as (balanced) binary trees7

## Answer

The algorithms are basically convert 2 input trees into 2 sorted lists, then
using 2 pointer approach to merge 2 lists.

```scheme
(define (intersect a b)
  (define (collect la lb)
    (if (or (null? la) (null? lb))
        null
        (let ([x (car la)] [y (car lb)])
          (cond
            [(= x y) (cons x (collect (cdr la) (cdr lb)))]
            [(> x y) (collect la (cdr lb))]
            [else (collect (cdr la) lb)]))))
  (list->tree (collect (tree->list-no-append a)
                       (tree->list-no-append b))))

(define (union a b)
  (define (collect la lb)
    (cond
      [(null? la) lb]
      [(null? lb) la]
      [else
       (let ([x (car la)] [y (car lb)])
         (cond
           [(= x y) (cons x (collect (cdr la) (cdr lb)))]
           [(> x y) (cons y (collect la (cdr lb)))]
           [else (cons x (collect (cdr la) lb))]))]))
  (list->tree (collect (tree->list-no-append a)
                       (tree->list-no-append b))))
```
